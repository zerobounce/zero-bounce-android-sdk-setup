apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

tasks.register('javadocJar', Jar) {
    dependsOn dokkaJavadoc
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'The ZeroBounce SDK for Android'
                    url = 'https://github.com/zerobounce/zero-bounce-android-sdk-setup'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                          id = 'zerobounce'
                          name = 'Zero Bounce'
                          email = 'integrations@zerobounce.net'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/zerobounce/zero-bounce-android-sdk-setup.git'
                        developerConnection = 'scm:git:ssh:/github.com/zerobounce/zero-bounce-android-sdk-setup.git'
                        url = 'https://github.com/zerobounce/zero-bounce-android-sdk-setup/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}