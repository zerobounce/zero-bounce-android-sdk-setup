import org.jetbrains.dokka.gradle.DokkaTask
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id("org.jetbrains.dokka") version "2.0.0"
}

ext {
    PUBLISH_GROUP_ID = 'com.zerobounce.android'
    PUBLISH_VERSION = '1.3.0'
    PUBLISH_ARTIFACT_ID = 'zerobouncesdk'
}

tasks.withType(DokkaTask).configureEach {
    dokkaSourceSets {
        named("main") {
            outputDirectory.set(layout.buildDirectory.dir.resolve("javadoc").get().asFile)
//            noAndroidSdkLink.set(false)

            // Do not create index pages for empty packages
            skipEmptyPackages.set(true)

            // Do not output deprecated members. Applies globally, can be overridden by packageOptions
            skipDeprecated.set(false)

            // No default documentation link to kotlin-stdlib
            noStdlibLink.set(false)
        }
    }
}

android {
    compileSdk = 36
    defaultConfig {
        minSdk 21
        targetSdk 36

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        test {
            resources.srcDirs += ['email_file.csv']
        }
        androidTest {
            resources.srcDirs += ['email_file.csv']
        }
    }
    buildFeatures {
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_17
        }
    }

    namespace 'com.zerobounce.android'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.squareup.okhttp3:okhttp:5.2.1'
    implementation 'com.google.code.gson:gson:2.13.2'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'

    testImplementation 'com.squareup.okhttp3:mockwebserver:5.2.1'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.powermock:powermock-core:2.0.9'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'

    dokkaJavadoc.dependsOn(':zero_bounce_sdk:generateReleaseRFile')
}
repositories {
    mavenCentral()
}

tasks.withType(Javadoc).configureEach {
    options.addBooleanOption('Xdoclint:none', true)
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"
